if _OS_ != "WINDOWS" && _OS_ != "LINUX" && _OS_ != "APPLE" {
	println("OS is not supported");
	exit(1);
}

fn join(arr) {
	res := "";
	for i := 0; i < len(arr); i+=1 {
		append(res, arr[i]);
		if i < len(arr) - 1 ->
			append(res, " ");
	}

	return res;
}

fn path(...) {
	res := "";
	del := "";

	for i := 0; i < len(_VA_ARGS_); i+=1 {
		append(res, _VA_ARGS_[i]);

		if _OS_ == "LINUX" || _OS_ == "APPLE" -> del = "/";
		else if _OS_ == "WINDOWS"             -> del = "\\";

		if i < len(_VA_ARGS_) - 1 ->
			append(res, del);
	}

	return res;
}

cmd_debug_mode := false;
fn cmd(...) {
	command := join(_VA_ARGS_);
	println("[CMD] ", command);
	if cmd_debug_mode == false ->
		system(command);
}

fn make_dir(dir_name) {
	if _OS_ == "LINUX" || _OS_ == "APPLE" {
		cmd("mkdir", "-p", dir_name);
	} else if _OS_ == "WINDOWS" {
		cmd("md", dir_name);
	}
}

fn copy_file(src, dst) {
	if _OS_ == "LINUX" || _OS_ == "APPLE" {
		cmd("cp", src, dst);
	} else if _OS_ == "WINDOWS" {
		cmd("copy", src, dst);
	}
}

fn SO() {
	if      _OS_ == "LINUX"   => ".so";
	else if _OS_ == "WINDOWS" => ".dll";
	else if _OS_ == "APPLE"   => ".dylib";
}

build_folder := path("build", "epsilang");
output_file := path(build_folder, "lib", "libepsl") + SO();

srcs := [
	path("src", "api.c"),
	path("src", "print.c"),
	path("src", "lexer.c"),
	path("src", "parser.c"),
	path("src", "eval.c"),
	path("src", "stdlib.c"),
];

make_dir("build");
make_dir(build_folder);
make_dir(path(build_folder, "include"));
make_dir(path(build_folder, "lib"));
copy_file(path("include", "api.h"), path(build_folder, "include", "epsl_api.h"));

cmd(
	"cc",
	"-fPIC",
	"-shared",
	"-std=c99",
	"-O3",
	"-o", output_file,
	join(srcs)
);
